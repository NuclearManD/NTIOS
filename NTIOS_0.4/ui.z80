; ui.z80 : user interface tools and functions, 1711150917
print_memory:
	push hl
	push de
	push bc
	ld a, l
	and $F0		; ensure that it is the beginning
	ld l, a
	ld a, ' '
	call send
	call send
	call send
	call send
	ld a, '|'
	call send
	push hl
	ld hl, hex_table
.write_head:
	ld a, (hl)
	or a
	jr z, .write_done
	inc hl
	call send
	ld a, ' '
	call send
	call send
	jp .write_head
.write_done:
	ld a, 13
	call send
	ld c, free_text_rows-1 ; number of rows to output
	pop hl
.row_start:
	push hl
	ld a, h
	call byte_to_hex
	call ostream
	pop hl
	push hl
	ld a, l
	call byte_to_hex
	call ostream
	ld a, '|'
	call send
	pop hl
.row_loop:
	ld a, (hl)
	push hl
	call byte_to_hex
	call ostream
	ld a, ' '
	call send
	pop hl
	inc hl
	ld a, l
	and $F
	jp nz, .row_loop
	
	ld a, 13
	call send
	dec c
	ld a, c
	or a
	jp nz, .row_start
	pop bc
	pop de
	pop hl
	ret